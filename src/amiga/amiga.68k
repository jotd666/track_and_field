*
*	Amiga-specific portions of Gyruss
*
* (c) JOTD 2025
*

	* first instruction (includes contain code)
	jra		start

	.include "../src/gyruss.inc"
	.include "../src/gyruss_sub.inc"
	.include "../src/data.inc"
	.include "hardware/custom.i"
	.include "exec/types2.i"
	.include "lvo/exec_lvos.i"
	.include "lvo/dos_lvos.i"
	.include "lvo/graphics_lvos.i"
	.include "mixer.inc"

AttnFlags = 0x128
AFB_68010 = 0
AFB_68020 = 1
AFB_68030 = 2

NO68020 = 1


BORDERBLANK = 1

* we plot pixels instead of star sprites
* star sprites are 16x8 and pixel in star sprite
* is at the center. Add shift to make position right
STAR_PIXEL_X = 8
STAR_PIXEL_Y = 4

SPRITE_X_CORRECTION = 7
SPRITE_Y_CORRECTION = 0
SPRITE_HEIGHT = 8

Y_MAX = 256

_custom = 0xDFF000

BASE_BPLCON2_VALUE = 0x0064		| sprites above all playfields






	.ifdef	RELEASE
OPT_ENABLE_PROFILING = 0         | DO NOT CHANGE THIS
OPT_ENABLE_LOGGING = 0           | DO NOT CHANGE THIS

	.else
OPT_ENABLE_LOGGING = 1
OPT_ENABLE_PROFILING = 0
* set to higher values than 1 to speed game up


	.endif

* to save bandwidth, do not clear cookie cut blit
* if no bitplane data: there will be some color artefacts
* but not noticeable unless 2 very different not very small
* ships cross each other paths, which doesn't happen very often
OPT_IGNORE_ZERO_BITPLANE = 1

	
OPT_ENABLE_DOUBLE_BUFFERING = 1


OPT_ENABLE_MULTIPLEXED_SPRITES = 0

NB_PLAYFIELD_PLANES = 5
NB_BOB_PLANES = 4		| only using 16 first colors
TOTAL_NB_COLORS = 1<<NB_PLAYFIELD_PLANES
NB_BYTES_PER_ROW = 40

* add 16 so sprites can be drawn at the bottom
* without corrupting the screen
NB_LINES = Y_MAX+16
NB_PLAYFIELD_LINES = NB_LINES


SCREEN_PLANE_SIZE = NB_BYTES_PER_ROW*NB_PLAYFIELD_LINES
RESTORE_SCREEN_PLANE_SIZE = SCREEN_PLANE_SIZE
RESTORE_SCREEN_SIZE = RESTORE_SCREEN_PLANE_SIZE*NB_BOB_PLANES

SCREEN_SIZE = SCREEN_PLANE_SIZE*NB_PLAYFIELD_PLANES
NB_HOST_SPRITES = 8

* hardware allows 48 but game implements some sort of multiplexing, allowing 128
* sprites at the same time (in theory), which is awesome!
NB_TARGET_SPRITES = 0x200/4  
	
	STRUCTURE	TARGET_SPRITE,0
	UBYTE	TARGET_SPRITE_Y
	UBYTE	TARGET_SPRITE_CODE1
	UBYTE	TARGET_SPRITE_CODE2
	UBYTE	TARGET_SPRITE_X
	LABEL	TARGET_SPRITE_SIZEOF

	STRUCTURE	PREVIOUS_SPRITE,0
	UWORD	PREV_X
	UWORD	PREV_Y
	UWORD	PREV_HEIGHT			
	UWORD	PREV_WIDTH			
	UBYTE	PREV_STAR			
	UBYTE	PREV_ACTIVE_PLANES	
	LABEL		PREVIOUS_SPRITE_SIZEOF

* hardware sprite (not used)
	STRUCTURE	SPRITE,0
	UBYTE	SPRITE_X
	UBYTE	SPRITE_Y
	UBYTE	SPRITE_CODE
	UBYTE	SPRITE_CLUT
	LABEL	SPRITE_SIZEOF




BIT_INSERT_COIN_1 = 0
BIT_INSERT_COIN_2 = 1
BIT_START_1P = 3
BIT_START_2P = 4

BIT_LEFT = 0
BIT_RIGHT = 1
BIT_UP = 2
BIT_DOWN = 3
BIT_FIRE = 4

.macro	GET_Z80_RAM_START_IN_A6	
	.ifdef	RELEASE
	lea		z80_ram_buffer,a6
	.else
	move.l		z80_ram_start_8000,a6
	.endif
	.endm
	
.macro	GET_M6809_MEMORY_START_IN_A6
	move.l	m6809_direct_page_pointer,a4
	.ifdef	RELEASE
	lea		m6809_ram_buffer-0x4000,a6
	.else
	move.l		m6809_ram_start_4000,a6
	
	* adjust offset to avoid subbing at run-time
	sub.w	#0x4000,a6
	.endif
	.endm
	
.macro	WAIT_BLIT
	move.w	#0x8400,(dmacon,a5)		| blitter high priority
wait\@:
	BTST	#6,(dmaconr,a5)
	BNE.S	wait\@
	move.w	#0x0400,(dmacon,a5)		| blitter normal priority
.endm

.macro MUL_TABLE value,len
mul\value\()_table:
	.set    mulv,0
	.rept	\len
	dc.w	mulv
	.set	mulv,mulv+\value
	.endr
.endm


* debug macro that colors the screen until LMB pressed
.macro 	blitz_bypass
	move.w	d0,-(a7)
	clr.w	d0
loop\@:
	move.w	d0,_custom+color
	btst	#6,0xbfe001
	beq.b	out\@
	addq.w	#1,d0
	bra.b	loop\@
out\@:
	move.w	(a7)+,d0
	nop
.endm

* same thing but if LMB is pressed when entering, macro waits for release)
.macro blitz
w\@:
	btst	#6,0xbfe001
	beq.b	w\@
	blitz_bypass
.endm

start:
	move.l	a7,old_stack
	jra	_user
    
	
* exports

	.global		_user
	.global		_player_start
	.global		_demo_end
	.global		_resload
	.global		_dosbase
	.global		cheat_used
* osd layer		
		.global osd_interrupt_control
		.global osd_break		| for debug	
		.global osd_blitz		| for debug
		.global osd_w_videoram
		.global osd_read_dsw_1
		.global osd_read_dsw_2
		.global osd_read_dsw_3
		.global osd_read_inputs_1
		.global osd_read_inputs_2
		.global osd_read_system_inputs
		
		.global	osd_queue_sprite
		.global	osd_queue_sprites
		.global	osd_draw_sprites
		* read/write on memory
		.ifndef	RELEASE
		.global	osd_log_cpu
		.global	osd_read_file
		.global	osd_write_file
		.global	osd_dump_logs
		.global	osd_cpu_logging_control
		.endif


		.global	is_game_playing
		
	.include	"ReadJoypad.i"
	.include	"whdload_funcs.i"
		.text


	.ifne	OPT_ENABLE_PROFILING
	* init profiling if enabled
	.include	"profiler.68k"
	.endif

	    
_user:
    * if D0 contains "WHDL"
    * A0 contains resload
        
    cmp.l   #0x05748444c,D0	| WHDL
    bne.b   .standard
	move.b	d1,_keyexit
    move.l a0,_resload

	move.l	a0,a2
	lea	(_tag,pc),a0
	jsr	(resload_Control,a2)
	
    bra		.no_forbid
.standard:
	
    * open dos library, graphics library
    move.l  0x4.W,a6
    lea dosname,a1
    moveq.l #0,d0
    jsr _LVOOpenLibrary(a6)
    move.l  d0,_dosbase

	lea	read_args_string(pc),a0
	lea	program_args(pc),a1
	move.l	#program_args_end-program_args,d0
	lsr.l	#2,d0		| number of longs
	

	bsr	get_args_BCPL

0:	
	lea		program_args,a0

	move.l	(A0)+,D0	| INVINCIBLE/S
	beq.b	1f
	or.l	#1,cheat_flags
	st.b	cheat_used
1:
	move.l	(A0)+,D0	| INFLIVES/S
	beq.b	2f
	or.l	#2,cheat_flags
	st.b	cheat_used
2:

	move.l	(A0)+,D0	| CHEATKEYS/S
	beq.b	2f
	* cheat isn't used as long as no cheat key is used
	or.l	#0x10,cheat_flags
2:

	move.l	(A0)+,D0	| STARTLIVES/K/N
	beq.b	2f
	bsr		bcpl_string_to_int
	subq	#3,d0
	jmi		2f
	move.l	d0,start_lives
2:
	move.l	(A0)+,D0	| SKILL/K/N
	beq.b	2f
	bsr		bcpl_string_to_int
	move.l	d0,difficulty_level
2:
	move.l	(A0)+,D0	| STARTLEVEL/K/N
	beq.b	2f
	bsr		bcpl_string_to_int
	move.l	d0,start_level_option
2:


	lea		_custom,a5
	lea		debug_copperlist,a0
	move.l	a0,cop1lc(a5)

* no multitask
	
	move.l	4.W,A6
	* system-friendly PAL/NTSC detect
	cmp.b #50,0x212(a6)		| vblankfrequency
	jeq	0f
	* set NTSC
	move.l	#NTSC_MONITOR_ID,monitor
0:
	lea graphicsname,a1
    moveq.l #0,d0
    jsr _LVOOpenLibrary(a6)
    move.l  d0,_gfxbase
    move.l	d0,a6
	move.l	38(a6),old_syscoplist
	move.l	34(A6),old_actiview		| gb_ActiView

	sub.l	a1,a1
	jsr	_LVOLoadView(a6)
	jsr	_LVOWaitTOF(a6)
	jsr	_LVOWaitTOF(a6)
	
	jbsr		load_highscores
    * check if "floppy" file is here
    move.l  _dosbase(pc),a6
    move.l   #floppy_file,d1
    move.l  #1005,d2   | MODE_OLDFILE
    jsr     _LVOOpen(a6)
    move.l  d0,d1
    beq.b   .no_floppy
    * "floppy" file found
    jsr     _LVOClose(a6)
    * wait 2 seconds for floppy drive to switch off
    move.l  #100,d1
    jsr     _LVODelay(a6)
.no_floppy:

* no multitask
	
    move.l  4,a6
    jsr _LVOForbid(a6)

    
	sub.l	A1,A1
	jsr	_LVOFindTask(a6)		| find ourselves
	move.l	D0,A0
	move.l	#-1,184(A0)	| pr_WindowPtr: no more system requesters (insert volume, write protected...)

.no_forbid:

	lea		_custom,a5
	lea		debug_copperlist,a0
	move.l	a0,cop1lc(a5)
	
	lea		bg_screen_buffer_1,a0
	addq	#2,a0	| leave 16 bits hidden for left side clipping
	move.l	a0,bg_screen_data_1
	move.l	a0,playfield_drawed_screen_ptr
	move.l	a0,playfield_displayed_screen_ptr



	.ifne	OPT_ENABLE_DOUBLE_BUFFERING
	lea		bg_screen_buffer_2,a0
	addq	#2,a0	| leave 16 bits hidden for left side clipping
	move.l	a0,bg_screen_data_2
	move.l	a0,playfield_displayed_screen_ptr
	.endif
		
	* set them even if reset by game afterwards
	* avoiding ugly stripe bitplane effect at startup

	bsr		set_static_bitplanes

	move.w	#TOTAL_NB_COLORS,d0
	lea		palette,a0
	lea		game_colors,a1
	bsr		load_palette
	

	move.l	cheat_flags,d0
	jeq		1f
	move.b	d0,d1
	and.b	#3,d1
	jeq		2f
	* cheat key disable highscore only if used
	st.b	cheat_used
2:
	btst	#0,d0
	sne		invincible_flag

	BTST	#1,d0
	sne		infinite_lives_flag
	btst	#2,d0

0:
1:
	move.b	difficulty_level+3,d0
	and.b	#3,d0
	
	moveq	#0,d0
	move.b	start_level_option+3,d0
	and.b	#0x3F,d0	| safety
	lea		start_level_table,a0
	move.b	(a0,d0.w),start_level
	
	move.l	misc_options,d1
	btst	#0,d1
	sne		frameskip
	btst	#1,d1
	sne		stars_off_flag
	.ifndef	RELEASE
	btst	#31,d1
	sne		break_at_startup_flag
	.endif
	
	**move.b	d0,dip_switches_2


	

	move.l	cheat_flags,d0
	btst	#0,d0
	beq.b	0f
	st		cheat_used
	*bset	#6,dip_switches_2
0:
	btst	#1,d0
	beq.b	0f
	st		infinite_lives_flag
	st		cheat_used
0:
	btst	#2,d0
	beq.b	0f
	st		cheat_used
0:
	move.l	cheat_flags,d0
	btst	#3,d0
	beq.b	0f
	st		cheat_used
	*bset	#5,dip_switches_2
0:
	
	move	#0x80,d1	| demo sounds on always
	move.l	start_lives(pc),d0
	or.b	d0,d1
	move.l	difficulty_level(pc),d0
	ror.b	#4,d0
	or.b	d0,d1
	
	move.b	d1,dip_switches_2
	
	jsr	 _detect_controller_types 
	clr.b	controller_joypad_0
	clr.b	controller_joypad_1

	jra		demo_game


	
* < D0: bcpl string (with leading size byte)
* < A1: dest C string
bcpl_arg_string_copy:
    movem.l D0-D2/A0-A1,-(a7)
	lsl.l	#2,d0
	moveq.l	#0,d2
    move.l  d0,a0
	move.b	(a0)+,d2	| size
	beq.b	1f
	subq	#1,d2
0:
	move.b	(a0)+,(a1)+
	dbf		d2,0b
1:
    movem.l (a7)+,D0-D2/A0-A1
    rts


bcpl_string_to_int:
    movem.l A0/A1,-(a7)
    lea temp_filename_buffer(pc),a1
    bsr bcpl_arg_string_copy
    * convert to integer
    move.l  a1,a0
    * < A0: pointer on C string
    * > D0: value
    * > D1: -1 if ok, position of the string if error
    bsr parse_integer
    movem.l  (a7)+,a0/a1
	rts
	
* parse integer from string
* < A0: pointer on C string
* > D0: value
* > D1: -1 if ok, position of the string if error
parse_integer:
    movem.l  d2/d3,-(a7)
    * go to end of string
    moveq.l #-1,d1
.loop1:
    addq.l  #1,d1
    tst.b   (a0,d1.w)
    bne.b   .loop1
    * d1 is the number of chars
    moveq.l #0,d0
    moveq.l #0,d2
    subq.l  #2,d1   | 10th power minus 1
.loop2:
    move.b  (a0)+,d2
    beq.b   .out
    
    cmp.b   #32,d2
    beq.b   .skip
    sub.b   #48,d2
    bcs.b   .error
    cmp.b   #10,d2
    bcc.b   .error
    move.w  d1,d3
    bmi.b   .doadd
.muloop:
    mulu    #10,d2
    dbf d3,.muloop
.doadd:
    add.l   d2,d0
.skip:
    subq.l  #1,d1
    bra.b   .loop2
.out:    
    movem.l  (a7)+,d2/d3
    rts
.error:
    moveq.l #0,d0
    bra.b   .error
	
    * thanks Toni for this 1.3 read argument code
BCPL_RdArgs = 78
	
	* a1 = pointer to result array. Must be LONG aligned!
	* a0 = formatting string. BSTR!
	* d0 = size of result array (number of LONGs)
get_args_BCPL:
	movem.l d2/d3/d4,-(sp)
	move.l d0,d3
	moveq #BCPL_RdArgs,d0
	move.l a0,d1
	lsr.l #2,d1
	move.l a1,d2
	lsr.l #2,d2
	moveq #0,d4
	bsr.s call_bcpl
	movem.l (sp)+,d2/d3/d4
	rts

	* d0 = gv index
	* d1-d4 = bcpl parms

BCPL_STACK = 3000

call_bcpl:
	movem.l d2-d7/a2-a6,-(sp)

	move.l d0,d6
	move.l d1,d5

	move.l 4.w,a6
	move.l	_dosbase(pc),a5


	sub.l a1,a1
	jsr	_LVOFindTask(a6)
	move.l d0,a4

	* allocate BCPL stack
	move.l #BCPL_STACK,d0
	move.l #65536+1,d1
	jsr	_LVOAllocMem(a6)
	move.l d0,d7
	beq.s 0f
	
	movem.l d7/a5/a6,-(sp)

	moveq #0,d0
	move.l d5,d1
	sub.l a0,a0
	move.l d7,a1
	lea 3*4(a1),a1
	move.l 136(a4),a2
	add.w	d6,d6
	add.w	d6,d6
	move.l 0(a2,d6.w),a4
	movem.l 46(a5),a5/a6
	jsr (a5) | call bcpl!
	
	movem.l (sp)+,d7/a5/a6

0:
	move.l d7,a1
	move.l #BCPL_STACK,d0
	jsr	_LVOFreeMem(a6)


	movem.l (sp)+,d2-d7/a2-a6
	rts



* < D0: sprite X
* < D1: sprite Y
* < D2: sprite code & flags
* < D3: sprite color (clut index, no flip information)
* > D6: allocated sprite index
alloc_sprite:
	* first pass: find a free sprite next to a sprite with
	* same clut
	lea		host_sprites(pc),a2
	moveq	#0,d6
0:
	cmp.b	(SPRITE_CLUT,a2),d3
	bne.b	10f
	* allocated (else would be -1/invalid) + same clut
	* check even/odd index
	btst	#0,d6
	beq.b	1f
	* odd: check if previous entry is free
	tst.b	(SPRITE_X-SPRITE_SIZEOF,a2)
	beq.b	8f
	tst.b	(SPRITE_Y-SPRITE_SIZEOF,a2)
	bne.b	10f
8:
	* previous entry is free, same clut: we can use that
	* entry, no color conflict
	lea		(-SPRITE_SIZEOF,a2),a2
9:
	move.b	d0,(a2)+		| X
	move.b	d1,(a2)+		| Y
	move.b	d2,(a2)+		| code
	move.b	d3,(a2)+		| clut (no flip information)
	rts						| done
1:
	* even: check if next entry is free
	tst.b	(SPRITE_X+SPRITE_SIZEOF,a2)
	beq.b	2f
	tst.b	(SPRITE_Y+SPRITE_SIZEOF,a2)
	bne.b	10f
2:
	* next entry is free, same clut: we can use that
	* entry, no color conflict
	lea		(SPRITE_SIZEOF,a2),a2
	bra.b	9b
10:
	* next entry
	addq.w	#SPRITE_SIZEOF,a2
	addq	#1,d6
	cmp.w	#NB_HOST_SPRITES,d6
	bne.b	0b
	* second pass if first pass failed (could not match
	* a pair with one occupied slot with compatible clut,
	* which happens when there are very few sprites
	* displayed): we have to find 2 consecutive free slots

	lea		host_sprites(pc),a2
	moveq	#0,d6
11:
	* we consider that a sprite is not allocated
	* if one of the coordinates is zero (real game
	* doesn't care and sets X to 255, and Y to 0 at times
	tst.b	(SPRITE_Y,a2)
	beq.b	12f
	tst.b	(SPRITE_X,a2)
	bne.b	20f
12:
	* not allocated: test odd sprite
	tst.b	(SPRITE_Y+SPRITE_SIZEOF,a2)
	jeq	9b
	tst.b	(SPRITE_X+SPRITE_SIZEOF,a2)
	jeq	9b		| ok, both slots are free, allocate the first one
20:
	* next sprite pair
	lea		(2*SPRITE_SIZEOF,a2),a2
	addq	#2,d6
	cmp.w	#NB_HOST_SPRITES,d6
	bne.b	11b
	
	* third pass: last chance/emergency: there ARE free slots
	* but maybe too many different cluts so we can't find a suitable slot.
	* In that case, pick the first free slot
	*
	lea		host_sprites(pc),a2
	moveq	#0,d6
21:
	tst.b	(SPRITE_X,a2)
	beq.b	9b		| ok, a slot is free, allocate it
	tst.b	(SPRITE_Y,a2)
	beq.b	9b		| ok, a slot is free, allocate it
30:
	* next sprite
	lea		(SPRITE_SIZEOF,a2),a2
	addq	#1,d6
	cmp.w	#NB_HOST_SPRITES,d6
	bne.b	21b
	* just cannot happen
	* if we reach that point there's a big problem, break if dev mode
	* hopefully if we save buffer in A1 we can reproduce the issue at will!
	.ifndef	RELEASE
	lea			sprite_buffer(pc),a1
	lea			sprite_allocation_error,a0
	jbsr	osd_break
	.endif
	* in release mode, just discard this sprite, maybe we'll get away with it!
	rts


demo_game:
		move		#0x4000,_custom+intena						| disable interrupts
		move		#0x7FFF,_custom+intreq						| ack all interrupts
		bsr			platform_init					| amiga-specific init
		
0:	
	.ifndef	RELEASE
	* compute ram start for dev mode
	* align so real addresses LSW match actual arcade addresses
	* and lower 16-bit match original RAM offsets
	* (since memory is shared, Z80 addresses match except for
	* ram after 0xA000 and M6809 addresses are slightly shifted)
	*
	* only the game logic & screen memory (0x8000-0x9FFF) is 
	* perfectly aligned but that's enough to debug the game logic
	* now that the 6809 code works
	*
	lea		dev_ram_buffer,a6
	move.l	a6,d0
	clr.w	d0
	move.l	d0,d1
	add.l	#0x18000,d1
	move.l	d1,z80_ram_start_8000		| Z80 memory (0x8000-0x9FFF)
	
	move.l	d0,d1
	add.l	#0x1A000,d1
	move.l	d1,m6809_ram_start_4000		| 6809 memory (0x4000-0x6800) 0x6000/0xA000 shared with Z80

	* relocate rom tables just after shared RAM
	* we changed the ROM offset from E000 to 6800 too
	* so memory is contiguous (only 1 segment of memory RAM+ROM)
	* and 6809 memory access is way faster (no bank switching)
	move.l	d0,d1
	add.l	#0x1C800,d1
	move.l	d1,m6809_rom_start_e000
	
	* copy the ROM data

	move.l 	d1,a1
	move.w	#0x1FF,d0
	lea		sub_rom_base,a0
0:
	move.l	(a0)+,(a1)+
	dbf		d0,0b
	.else
	* release mode

	lea		m6809_rom_buffer,a1
	move.w	#0x1FF,d0
	lea		sub_rom_base,a0
0:
	move.l	(a0)+,(a1)+
	dbf		d0,0b
	.endif
	
	GET_M6809_MEMORY_START_IN_A6
	jbsr		gyruss_sub_reset				| initialize the subcpu memory
	* direct page won't change, let's store it now for irq
	move.l	a4,m6809_direct_page_pointer
	GET_Z80_RAM_START_IN_A6
	jmp			gyruss_reset					| pass control to the cpu core
		
	
* < D0.l sprite 4 byte code
osd_queue_sprite:
	move.l	a0,-(a7)
	move.l	queued_sprite_pointer,a0
	move.l	d0,(a0)+
	move.l	a0,queued_sprite_pointer
	.ifndef	RELEASE
	cmp.l 	#queued_sprite_buffer+0x200,a0
	jne		0f
	BREAKPOINT	"sprite buffer full"
0:
	.endif
	move.l	(a7)+,a0
	rts

osd_queue_sprites:
	movem.l	d0-a6,-(a7)
	GET_M6809_MEMORY_START_IN_A6
	jbsr	gyruss_sub_queue
	movem.l	(a7)+,d0-a6
	rts
	
osd_draw_sprites:
	movem.l	d0-a6,-(a7)
	move.l	#queued_sprite_buffer,queued_sprite_pointer
	GET_M6809_MEMORY_START_IN_A6
	jbsr	gyruss_sub_draw	
	movem.l	(a7)+,d0-a6
	rts
	
do_both_gyruss_irq:
	* set base for RAM
	lea	rom_base,a5
	GET_Z80_RAM_START_IN_A6
	jra			gyruss_irq					| pass control to the cpu core
	
* just in case some hardware calls nmi
_nmi:
	move.w	#0x7FFF,_custom+intreq
	move.w	#0x7FFF,_custom+intreq
	rts
	
_vblank:
    movem.l d0-d7/a0-a6,-(a7)
	lea		_custom,a5
	move.w	(intreqr,a5),d0
	btst	#5,d0
	beq.b		1f

	clr.b	vbl_sync_flag
	
	move.l	_joypad_state(pc),d1
	moveq	#1,d0
	jsr	_read_joystick
	move.l	d0,_joypad_state
	
	* CD32 "play" is disabled but it's free to read
	* 3rd button on a Sega 3-button so 3rd button is pause
	btst	#JPB_BTN_PLAY,d0
	beq.b	0f
	btst	#JPB_BTN_PLAY,d1
	bne.b	0f
	bsr		toggle_pause
0:
	* here music doesn't loop. We have to stop it when it ends
	move.w	music_tick,d1
	beq.b	13f
	bmi.b	13f		| negative: infinite, not interruptible
	
	subq	#1,d1

	move.w	d1,music_tick
	bne.b	13f
	jbsr		osd_music_stop
13:
	
	* ack vbl twice (040/pistorm bug)
	move.w	#0x20,(intreq,a5)
	move.w	#0x20,(intreq,a5)	
	jra	3f
1:
	* has to be copper

	* we're going to ack copper & software interrupt now
	* do it twice (040/pistorm bug)
	move.w	#0x14,(intreq,a5)
	move.w	#0x14,(intreq,a5)

	tst.b	pause_flag
	jne	23f



	* declare that we're in interrupt mode so osd_disable_interrupts/osd_enable_interrupts
	st.b	interrupt_mode
	* is level 1 interrupt set? that would mean that mid-screen interrupt
	* is the source of the interrupt
	btst	#2,d0
	beq.b	2f
	* mid-screen copper interrupt, must occur once every 5 times
	* to simulate ~60 Hz on 50 Hz display
	move.b	mid_screen_interrupt_count,d0
	addq.b	#1,d0
	cmp.b	#5,d0
	bne.b	1f
	jbsr		do_both_gyruss_irq
	clr.b	interrupt_mode	
	clr.b	d0
1:
	move.b	d0,mid_screen_interrupt_count
	jra		3f
2:


	
	subq.w	#1,delay_timer
		
	jbsr		do_both_gyruss_irq
	
	tst.b	frameskip
	jeq		0f
	eor.b	#1,flip_flop
	jeq		23f
0:
	bsr		really_update_tiles
	bsr		really_update_sprites
	
	
	.ifne	OPT_ENABLE_DOUBLE_BUFFERING
	bsr		switch_screen_buffers
	.endif
	
23:


3:
    movem.l (a7)+,d0-d7/a0-a6
    rte
	
	.ifndef	RELEASE

osd_log_cpu:
	move.l	a6,-(a7)
	tst.w	(8,a7)
	jne		0f
	tst.b	cpu_log_enable
	jeq		445f
	* check if cpu log is enabled
	move.l	log_ptr,a6
	move.w	(10,a7),(a6)+	| the PC
	move.b	d0,(a6)+
	move.b	d1,(a6)+
	move.b	d2,(a6)+
	move.b	d3,(a6)+
	move.b	d4,(a6)+
	move.b	d5,(a6)+
	move.b	d6,(a6)+
	move.b	d7,(a6)+
	move.l	a0,(a6)+
	move.l	a1,(a6)+
	move.l	a2,(a6)+
	move.l	a3,(a6)+
	move.w	#0xDEAD,(a6)+
	cmp.w	#0xCAFE,(a6)  | hitting the protection buffer
	jne		444f
	* save log before hitting breakpoint
	move.l	(a7)+,a6
	jbsr	save_cpu_log
	BREAKPOINT	"cpu log buffer full!"
	illegal
444:
	move.l	a6,log_ptr
445:
	move.l	(a7)+,a6
	rts

* sub-cpu
0:
	* check if sub cpu log is enabled
	tst.b	cpu_log_enable+1
	jeq		445f
	move.l	sub_log_ptr,a6
	move.w	(10,a7),(a6)+	| the PC
	move.l	d1,-(a7)
	rol.w	#8,d1
	move.b	d0,d1
	rol.w	#8,d1   | make D, make sure that not too many diffs
	move.w	d0,(a6)+
	move.w	d1,(a6)+
	move.w	d2,(a6)+
	move.w	d3,(a6)+
	move.w	d4,(a6)+
	cmp.w	#0xCAFE,(a6)  | hitting the protection buffer
	jne		444f
	BREAKPOINT	"sub cpu log buffer full!"
444:
	move.w	#0xDEAD,(a6)+
	move.l	a6,sub_log_ptr
	move.l	(a7)+,d1
445:
	move.l	(a7)+,a6
	rts

* < D6: 0 disable, 1 enable
* < D7: 0: main cpu, 1: sub cpu
osd_cpu_logging_control:
	lea		cpu_log_enable,a1
	and.w	#1,d7
	move.b	d6,(a1,d7.w)
	rts
	
* < A0: filename
* < D6: virtual address (16 bit)
* < D7: 0: main cpu, 1: sub cpu
osd_read_file:
	movem.l	d0-a6,-(a7)
	
	move.w	#resload_LoadFile,d4
	jra		osd_file_io
	
* < A0: filename
* < D5: length
* < D6: virtual address (16 bit)
* < D7: 0: main cpu, 1: sub cpu
* this is broken!

osd_write_file:
	ILLEGAL
	movem.l	d0-a6,-(a7)
	
	move.l	d5,d0
	move.w	#resload_SaveFile,d4
osd_file_io:
	move.l	_resload(pc),a2
	tst.b	d7
	jeq		0f
	* subcpu
	GET_M6809_MEMORY_START_IN_A6
	cmp.w	#0x6000,d6
	jcc		1f
	* 0x4000 rom part
	sub.w	#0x4000,d6
	add.w	d6,a6
	move.l	a6,a1
	jbsr	(a2,d4.w)
	jra		10f	
1:
	* 0x6000 rom part
	sub.w	#0x6000,d6
	add.w	d6,a5
	move.l	a5,a1
	jbsr	(a2,d4.w)
	jra		10f
0:
	* main cpu
	GET_Z80_RAM_START_IN_A6
	sub.w	#0x8000,d6
	add.w	d6,a6
	move.l	a6,a1
	jbsr	(a2,d4.w)
10:
	movem.l	(a7)+,d0-a6
	rts
	

	.endif


osd_blitz:
	blitz
	rts
	
osd_break:
	* sends a WinUAE command to enter WinUAE debugger
	.ifndef	RELEASE
	move.l	D0,-(a7)
	pea     0.w
	* Normally it would point to where the result of the command is written, but since the last parameter (first pushed)
	* indicating the number of bytes to write is 0, nothing is written.
	pea     0.w	
	pea     1003f-1002f
	pea     1002f(pc)
	pea     -1.w
	pea     82.w
	jsr     0xf0ff60
	lea     24(sp),sp
	move.l	(a7)+,d0
	.endif
    rts
		
1002: .asciz    "AKS_ENTERDEBUGGER 1"
1003:
        .align	2

	

* < D0: number of 1/60Hz ticks
* can work with interrupts enabled or disabled
osd_wait:
	move.w	d0,delay_timer
	**jbsr	osd_is_vblank_interrupt_enabled
	tst.w	d0
	jne		2f
	* interrupts are disabled: timer isn't going to change
	* wait using vertical blank interrupt request (we don't need it!)
	clr.w	one_out_of_five
0:
	move.w	#0x20,_custom+intreq
	move.w	#0x20,_custom+intreq
1:
	move.w	_custom+intreqr,d0
	btst	#5,d0
	beq.b	1b
	addq.w	#1,one_out_of_five
	cmp.w	#5,one_out_of_five
	bne.b	5f
	subq.w	#1,delay_timer	
	clr.w	one_out_of_five
5:
	subq.w	#1,delay_timer
	beq.b	4f
	bpl.b	0b
4:
	rts
	
2:
	tst.w	delay_timer
	bne.b	2b
	rts


*void gyruss_state::draw_sprites(bitmap_ind16 &bitmap, const rectangle &cliprect)
*{
*	for (int offs = 0xbc; offs >= 0; offs -= 4)
*	{
*		int const x = m_spriteram[offs];
*		int const y = 241 - m_spriteram[offs + 3];
*
*		int const gfx_bank = m_spriteram[offs + 1] & 0x01;
*		int const code = ((m_spriteram[offs + 2] & 0x20) << 2) | (m_spriteram[offs + 1] >> 1);
*		int const color = m_spriteram[offs + 2] & 0x0f;
*		int const flip_x = ~m_spriteram[offs + 2] & 0x40;
*		int const flip_y =  m_spriteram[offs + 2] & 0x80;
*
*		m_gfxdecode->gfx(gfx_bank)->transpen(bitmap, cliprect, code, color, flip_x, flip_y, x, y, 0);
*	}
*}
	
	
	
* 2 conditions I can think of:

* Z: set if not playing
is_game_playing:
	move.l	a6,-(a7)
	GET_Z80_RAM_START_IN_A6
	tst.b	game_in_play_9669(a6)
	move.l	(a7)+,a6
	rts
	
lose_all_lives:
	move.l	a6,-(a7)
	GET_Z80_RAM_START_IN_A6
	move.b	#1,lives_9600(a6)					| no more lives
	move.b	#0xf0,level_playing_flag_9000(a6)	| kill player
	move.l	(a7)+,a6
	rts

add_10000_points:
	move.l	a6,-(a7)
	GET_Z80_RAM_START_IN_A6
	movem.w	d0/d7,-(a7)
	move.b	player_1_score_9663+2(a6),d0
	MOVEQ	#1,d7
	abcd	d7,d0
	move.b	d0,player_1_score_9663+2(a6)
	movem.w	(a7)+,d0/d7
	move.l	(a7)+,a6
	rts

* put/clear pixel on one bitplane of the screen
* D0: X
* D1: Y
* D2: plane number (0-4)
* D3: nonzero: set, zero: clear
* A1: bitplanes

set_pixel:
	movem.l	d0-d4/a1/a2,-(a7)
	move.b	d0,d4
	lsr.w	#3,d0
	lea		mulNB_BYTES_PER_ROW_table,a2
	add.w	d1,d1
	move.w	(a2,d1.w),d1		| offset
	add.w	d0,d1
	add.w	d1,a1
	* add bitplane offset
	and.w	#0x7,d2
	add.w	d2,d2
	lea		mulPLANE_table(pc),a2
	add.w	(a2,d2.w),a1

	tst.b	d3
	jeq		2f
	* select proper bit
	and.b	#7,d4
	neg.b	d4
	add.b	#7,d4
	bset	d4,(a1)
	jra		3f
2:
	* clear (and also clear pixels around)
	clr.b	(a1)
3:
	movem.l	(a7)+,d0-d4/a1/a2
	rts
	
mulPLANE_table:
	.word	0
	.word	SCREEN_PLANE_SIZE
	.word	SCREEN_PLANE_SIZE*2
	.word	SCREEN_PLANE_SIZE*3
	.word	SCREEN_PLANE_SIZE*4
	
* what: blits data on bg plane, no horizontal shifting (erase)
* args:
* < A0: pristine background
* < A1: dest buffer
* < D0: X (rounded to smaller 8-multiple)
* < D1: Y
* < D2: width in bytes
* < D4: height

* trashes: D0-D1

blit_background:
	movem.l	d2-d6/a0-a5,-(a7)
	lea		_custom,a5
	* adjust offset X: X//8
	lsr.w	#3,d0
	lea		mulNB_BYTES_PER_ROW_table(pc),a4
	add.w	d1,d1
	* add Y offset: Y*40
	add.w	(a4,d1.w),d0
	* same offset for source and destination
	add.w	d0,a1
	add.w	d0,a0
	

    move.l  #0x09f00000,d3    | A->D copy, ascending mode	
	move.w	#NB_BYTES_PER_ROW,d1		| blit modulo

    sub.w   d2,d1       | blit modulo
    lsl.w   #6,d4
    lsr.w   #1,d2		| blit width
    add.w   d2,d4       | blit height
	
    * always the same settings

    * now just wait for blitter ready to write all registers
	WAIT_BLIT
    
    * blitter registers set

	move.l d3,bltcon0(a5)	| sets con0 and con1

    move.w  d1,bltamod(a5)	|A modulo
    move.w  d1,bltdmod(a5)	|D modulo
							
	move.l a0,bltapt(a5)	|source graphic top left corner
	move.l a1,bltdpt(a5)	|destination top (bottom) left corner
	move.w  d4,bltsize(a5)	|rectangle size, starts blit
	
	.rept	NB_BOB_PLANES-1
	lea	(RESTORE_SCREEN_PLANE_SIZE,a0),a0
	lea	(SCREEN_PLANE_SIZE,a1),a1
	WAIT_BLIT
	* second bitplane
	move.l a0,bltapt(a5)	|source graphic top left corner
	move.l a1,bltdpt(a5)	|destination top (bottom) left corner
	move.w  d4,bltsize(a5)	|rectangle size, starts blit
	.endr
	
	movem.l	(a7)+,d2-d6/a0-a5
	rts

* we're going to scan for dirty tiles (0x400 array)
* an optimization is to also have flags for 0x20-size regions
* if the value is 0, then no need to try to refresh the next
* 0x20 tiles. If screen tiles don't change, we read 0x20 bytes
* instead of 0x400 each frame.

really_update_tiles:
	lea		_custom,a5
	WAIT_BLIT
	GET_Z80_RAM_START_IN_A6
	lea		(0x400,a6),a5
	lea		tiles_row_flags(pc),a1
	moveq	#0,d3
3:
	tst.b	(a1)+
	jeq		2f
	lea		tiles_refresh_table(pc),a0
	subq.b	#1,(-1,a1)
	move.w	#0x1F,d2
	move.w	d3,d7
	lsl.w	#5,d7		| times 32, get offset for big dirty table
	add.w	d7,a0
0:
	tst.b	(a0)+
	jeq		1f
	subq.b	#1,(-1,a0)	| decrease dirty counter
	* read tile & color codes from RAM
	move.b	(a6,d7.w),d1
	move.b	(a5,d7.w),d0
	movem.l	d2/d3/d7/a0/a1/a5,-(a7)
	jbsr	update_bg_tile
	movem.l	(a7)+,d2/d3/d7/a0/a1/a5
1:
	addq	#1,d7
	dbf	d2,0b
2:
	addq	#1,d3
	cmp.w	#0x20,d3
	jne		3b
	rts
	

is_forced_restore:
	GET_Z80_RAM_START_IN_A6
	move.l	#0x83838383,d0
	* 4 locations where text could be printed, which means
	* that the game is not playing at full rate ("game over",
	* "ready", "2 warps to xxx", "planet name", "chance stage"
	* "bonus xxx" displayed
	* probably not a lot of sprites displaying 
	* when text is showing, so not a problem to blit
	* more stuff)
	
	cmp.b	(0x610,a6),d0		| planet if not a sprite (uranus!)
	jne		1f
	cmp.b	(0x614,a6),d0
	jne		1f
	cmp.b	(0x633,a6),d0
	jne		1f
	cmp.b	(0x670,a6),d0
	jne		1f
	cmp.l	(0x550,a6),d0
	jne		1f
	
	
	moveq	#0,d0
	rts
1:
	* some text is above the playfield: force restore
	st	d0
	rts
	
* < A0: target sprite pointer

really_update_sprites:
update_bobs_and_hw_sprites:

	jbsr	is_forced_restore
	move.b	d0,d6			| D6 = $FF: do restore anytime
	
	***********************
	* erase previous BOBs *
	***********************
	move.l	bob_previous_drawed_sprites(pc),a4
	lea		_custom,a5
	
	* clear previous positions
	lea	background_restore_buffer,a0
	move.l	playfield_drawed_screen_ptr(pc),a1
	move.w	#NB_TARGET_SPRITES-1,d7
1:
	move.l	(a4),d1	| read X and Y
	jeq		11f		| x=y=0: inactive, skip
	move.l	d1,d0
	swap	d0		| get X
	* clear area where the sprite was
	clr.l	(a4)+			| ack deletion
	move.w	(a4)+,d4		| height
	move.w	(a4)+,d2		| width/clut index (if star)
	move.b	(a4)+,d3		| set if star
	move.b	(a4)+,d5
	tst.b	d6
	jne		2f				| forced restore
	* not forced restore. That depends on sprite Y pos
	cmp.b	#24,d1
	jcs		2f
	cmp.b	#232,d1		| 248-16
	jcc		2f
	tst.b	d3
	jeq		21f
	* star: just remove pixel from proper plane
	moveq	#0,d3
	jbsr	set_pixel
	jra		4f
21:
	* 24 <= Y < 248 and no forced restore: we can just clear the background
	move.w	d6,-(a7)
	move.l	a1,a3
	moveq	#NB_BOB_PLANES,d3
	and.w	#0x1F0,d0		| align on 16 lower bound
	jbsr	clear_planes_any_blitter_internal
	move.l	a3,a1
	move.w	(a7)+,d6
	jra		4f
2:
	tst.b	d3
	jeq		21f
	moveq	#2,d2	| star: blit width = 2
21:
	and.w	#0x1F0,d0		| align on 16 lower bound
	jbsr		blit_background
	jra		4f
4:
	dbf		d7,1b	
	bra.b	12f
11:
	lea	(PREVIOUS_SPRITE_SIZEOF,a4),a4
	dbf		d7,1b
12:	

	
	*********************************************
	* draw current BOBs from 6809 sprite memory *
	*********************************************

	GET_M6809_MEMORY_START_IN_A6
	* original machine works that way. On the amiga, it
	* works more or less but sprite multiplexing cannot be
	* emulated so some blinking could occur
  	*lea		(0x40+NB_TARGET_SPRITES*4-4,a6),a0
	move.l	queued_sprite_pointer,a0
	lea		queued_sprite_buffer+NB_TARGET_SPRITES*4,a1
	* clear the rest
0:
	cmp.l	a1,a0
	jeq		1f
	clr.l	(a0)+
	jra		0b
1:
	lea		queued_sprite_buffer+0x1FC,a0

    lea _custom,A5
	move.l	bob_previous_drawed_sprites(pc),a6
	lea		sprite_groups(pc),a2
	lea		bob_table,a4
	moveq	#NB_TARGET_SPRITES-1,d7
1:
	jbsr	update_one_bob
	subq.w	#4,a0
	dbf		d7,1b
2:
	rts
	

* < A0: sprite array
* < A2: sprite group table
* < A4: bob table
* < A5: custom
* < A6: previously drawed sprites array
* must preserve all of those, plus D7

update_one_bob:
	* code = 0 and clut = 0 => disabled ?? to check
	moveq	#0,d0
	* game disables sprites on Y, but X=0 is a perfectly valid value
	
	moveq	#0,d1
	move.b	(TARGET_SPRITE_Y,a0),d1  | sprite Y
	jeq	2f			| Y set to 0: not shown
	move.b	(TARGET_SPRITE_X,a0),d0

	

	.ifne	SPRITE_Y_CORRECTION
	add.w	#SPRITE_Y_CORRECTION,d1
	.endif
	.ifne	SPRITE_X_CORRECTION
	add.w	#SPRITE_X_CORRECTION,d0
	.endif
	
	moveq	#0,d2
	moveq	#0,d3
	* code = ((m_spriteram[offs + 2] & 0x20) << 2) | (m_spriteram[offs + 1] >> 1)
	move.b	(TARGET_SPRITE_CODE1,a0),d2
	jeq		2f

	move.b	(TARGET_SPRITE_CODE2,a0),d3
	and.b	#0x20,d3
	add.w	d3,d3
	add.w	d3,d3
	lsr.b	#1,d2
	jcc		0f
	bset	#8,d3			| sprite bank 1 (add 0x100)
0:
	or.b	d2,d3
	
	move.b	(TARGET_SPRITE_CODE2,a0),d2
	* color = m_spriteram[offs + 2] & 0x0f
	and.b	#0xF,d2		| CLUT

	* stars displayed some other way
	cmp.b	#0x13,d3
	jeq		20f

	* here D3 is code (with bank) and D2 is clut
	* log the usage
	
	.ifne	OPT_ENABLE_LOGGING
	movem.l	a2/d2-d3,-(a7)
	lea		sprite_log_table,a2
	lsl.w	#4,d3		| 16 cluts
	add.w	d2,d3		| clut offset

	st.b	(a2,d3.l)
	movem.l	(a7)+,a2/d2-d3
	.endif

	move.w	#SPRITE_HEIGHT,d6
	* check if it is a grouped sprite in the table with 0x200 pre-computed entries
	tst.b	(a2,d3.w)
	jeq		10f
	* grouped 16x16 sprite. They have cleverly organized those by code
	* if not flipped 0x9A is displayed on top and 0x19A is displayed 8 pixels below
	* if flipped, it's the other way round
	btst	#6,(TARGET_SPRITE_CODE2,a0)
	jne		100f
	* flipped: only display 0x1xx sprite
	sub.w	#0x100,d3
	jcs		2f
	jra		101f
100:
	* not flipped: only display 0x0xx sprite
	cmp.w	#0x100,d3
	jcc		2f	
101:
	add.w	d6,d6		| double the height to add
	* end grouped 16x16 sprite
10:
	add.w	d3,d3
	add.w	d3,d3
	move.l	(a4,d3.w),d4		| get pointer from bob table
	jeq		2f
	* entry is valid, now what about clut?
	
	move.l	d4,a1
	add.w	d2,d2
	add.w	d2,d2
	move.l	(a1,d2.w),d4
	jeq		22f

	* re-load properties (for X/Y flip)
	move.b	(TARGET_SPRITE_CODE2,a0),d3
	move.l	a0,-(a7)
	move.l	d4,a0
	* flip bits are exactly like Pooyan
    * flip_x = ~D3 & 0x40
    * flip_y =  D3 & 0x80
	btst	#7,d3
	jeq		101f
	* mirror
	add.w	#((NB_BOB_PLANES+1)*4+8),a0
101:
	move.w	(a0)+,d4		| height
	move.w	(a0)+,d5		| width
	move.w	(a0)+,d2		| Y-offset
	btst	#6,d3
	jne		101f
	* flipped: offset is different: it's 8 or 16-height-offset
	neg.w	d2
	sub.w	d4,d2
	add.w	d6,d2		| tile height is 8 or 16 pixels
101:
	add.w	d2,d1

	* A0 points on the first bitplane of the bob

	move.l		playfield_drawed_screen_ptr(pc),a1
	
	* D0 and D1 are already set
	move.w	(a0)+,d6				| active plane mask
	move.l	(NB_BOB_PLANES*4,a0),d2
	jeq	21f		| mask is zero: blank tile => skip
	move.l	d2,a3
	moveq	#0,d2	| no y-clip
	* note down that this bob will have to be erased
	move.w	d0,(a6)+			| store current sprite attributes to previous ones
	move.w	d1,(a6)+			| store current sprite attributes to previous ones
	move.w	d4,(a6)+			| store height
	move.w	d5,(a6)+			| store width in bytes
	clr.b	(a6)+				| not a star
	move.b	d6,(a6)+		    | active planes (helps for erase)
	move.w	d5,d2			    | width in bytes, 4 for 16 pixels+shift
	moveq	#0,d5			    | Y-offset

	btst	#6,d3
	jne		101f
	* flip
	neg.w	d4
101:


	moveq	#NB_BOB_PLANES,d3		| 5 planes

	bsr		blit_planes_any_internal_cookie_cut
	move.l	(a7)+,a0
	rts
21:
	move.l	(a7)+,a0
2:
	lea		(PREVIOUS_SPRITE_SIZEOF,a6),a6
	rts
	* display star
20:
	move.l		playfield_drawed_screen_ptr(pc),a1
	subq	#4,d2	| cluts 4-8 => 0-4 bitplane numbers
	* note down that this bob will have to be erased
	addq	#STAR_PIXEL_X,d0	
	addq	#STAR_PIXEL_Y,d1	
	move.w	d0,(a6)+			| store current sprite attributes to previous ones
	move.w	d1,(a6)+			| store current sprite attributes to previous ones
	move.w	#1,(a6)+			| height = 1
	move.w	d2,(a6)+			| store clut instead of width in bytes
	move.w	#-1,(a6)+			| set "is a star" flag, all planes active (who cares)
	st.b	d3
	jbra	set_pixel

22:
	.ifndef	RELEASE
	lsr.w	#2,d2		| clut
	lsr.w	#2,d3		| code
	move.w	#0xF00,_custom+color
	.endif
	jra		2b

sprite_groups:
	.include	"sprite_groups.68k"
	
update_one_hw_sprite:
	* code = 0 and clut = 0 => disabled ?? to check
	moveq	#0,d2
	move.b	(TARGET_SPRITE_CODE1,a0),d2
	jeq	2f

	move.b	(TARGET_SPRITE_X,a0),d0
	add.w	d2,d2
	add.w	d2,d2
	btst    #2,(TARGET_SPRITE_CODE2,a0)
	jeq		0f
	addq	#2,d2
0:
	cmp.b	#1,(a6,d2.w)
	jne		2f		| not a hw sprite: zap
	* get the actual sprite index for our engine
	move.b	(1,a6,d2.w),d2
	and.w	#0xFF,d2
	* it is a HW sprite all right
	lsl.w	#4,d2	| 4 pointers for each sprite
	move.b  (TARGET_SPRITE_CODE2,a0),d3
	btst	#0,d3
	jeq		10f
	addq	#8,d2			| mirror sprite couple
10:
	lea		(a4,d2.w),a5	| pointer on first sprite

	moveq	#0,d0
	moveq	#0,d1
	move.b	(TARGET_SPRITE_Y,a0),d1  | sprite Y
	beq.b	2f			| Y set to 0: not shown
	not.b	d1

	move.b	(TARGET_SPRITE_X,a0),d0  | sprite X
	
	.ifne	SPRITE_Y_CORRECTION
	add.b	#SPRITE_Y_CORRECTION,d1
	.endif
	.ifne	SPRITE_X_CORRECTION
	add.b	#SPRITE_X_CORRECTION,d0
	.endif

	* D0 and D1 are already set

	sub.w	#16,d0	| move each sprite 16 bits to the left
	bmi.b	2f			| X < 0 after correction: can't show
	bsr		store_sprite_pos

	
	* first sprite of couple
	move.l	(a5)+,a3
	move.l	d0,(a3)
	move.l	a3,d1
	* store sprite pointer in copperlist
	move.w	d1,(4,a2)
	swap	d1
	move.w	d1,(a2)
	* second sprite of couple
	BSET	#7,d0		| attach bit set
	move.l	(a5),a3
	move.l	d0,(a3)
	move.l	a3,d1
	* store sprite pointer in copperlist
	move.w	d1,(4+8,a2)
	swap	d1
	move.w	d1,(8,a2)
	add.w	#0x10,a2	| advance in sprite copperlist part
2:
	
	rts

	.ifne	OPT_ENABLE_LOGGING
osd_dump_logs:
	move.w	sr,-(a7)
	move.w	#0x2700,SR
	movem.l	d0-a6,-(a7)
	move.l	_resload(pc),a2
	lea		sprite_log_table,a1
	lea		sprite_log_name(pc),a0
	move.l	#16*512,d0
	jbsr	resload_SaveFile(a2)
	
	move.l	_resload(pc),a2
	lea		character_log_table,a1
	lea		character_log_name(pc),a0
	move.l	#16*512,d0
	jbsr	resload_SaveFile(a2)
	
	GET_M6809_MEMORY_START_IN_A6
  	lea		(0x40,a6),a1
	lea		sprites_ram_name_4040(pc),a0
	move.l	#0x100-0x40,d0
	jbsr	resload_SaveFile(a2)
	
	GET_Z80_RAM_START_IN_A6
	lea		sprites_ram_name_A000(pc),a0
	lea	(0x2000,a6),a1		| sprites
	move.l	#0x200,d0
	jbsr	resload_SaveFile(a2)
	lea		sprites_ram_name_A200(pc),a0
	lea	(0x2200,a6),a1		| sprites
	move.l	#0x200,d0
	jbsr	resload_SaveFile(a2)
	
	jbsr	save_cpu_log
	
	lea		sub_log_buffer,a1
	move.l	sub_log_ptr,d0
	sub.l	a1,d0
	jeq		0f
	lea		sub_cpu_log_name(pc),a0
	jbsr	resload_SaveFile(a2)
	
0:
    movem.l (a7)+,d0-a6
	move.w	(a7)+,SR
	rts

save_cpu_log:
	move.l	_resload(pc),a2
	lea		log_buffer,a1
	move.l	log_ptr,d0
	sub.l	a1,d0
	jeq		0f
	move.l	log_ptr,a0
	* add RAM & ROM base in the end
	move.l	a6,(a0)+
	move.l	#rom_base,(a0)+
	addq	#8,d0
	lea		cpu_log_name(pc),a0
	jbsr	resload_SaveFile(a2)
0:	
	rts
	
sprites_ram_name_4040:
	.asciz	"sprite_ram_4040"
sprites_ram_name_A000:
	.asciz	"sprite_ram_A000"
sprites_ram_name_A200:
	.asciz	"sprite_ram_A200"
sprite_log_name:
	.asciz	"used_sprites"
character_log_name:
	.asciz	"used_tiles"
cpu_log_name:
	.asciz	"cpu_log"
sub_cpu_log_name:
	.asciz	"sub_cpu_log"
	.align	2
	.endif	
	
* what: blits 16x? data on one plane, cookie cut
* args:
* < A0: pointers on 16x? data
* < A1: plane  (40 rows)
* < A3: source mask for cookie cut (16x16)
* < D0: X
* < D1: Y
* < D2: source y-offset
* < D3: height of data
* trashes: D0-D1
blit_planes_cookie_cut_16x:
    lea _custom,A5
	move.w	d2,d5
	move.w	d3,d4
	moveq	#NB_BOB_PLANES,d3		| 4 planes
    moveq  #4,d2       | 16 pixels + 2 shift bytes
* < A5: custom
* < D0.W,D1.W: x,y
* < A0: source (pointer on array of planes)
* < A1: destination fg plane, also background to mix with cookie cut fg plane
* < A3: source mask for cookie cut
* < D2: width in bytes (inc. 2 extra for shifting)
* < D3: number of planes
* < D4: height. If negative, source is copied with negative modulo (flip)
* < D5: y offset for source planes
* < D7: blit mask (not considered, optim, -1 all through the game)
* blit mask set
* returns: start of destination in A1 (computed from old A1+X,Y)
* trashes: a1
blit_planes_any_internal_cookie_cut:
    movem.l d0-d7/a4,-(a7)
    * pre-compute the maximum of shit here
	tst.w	d4
	bpl.b	1f
	sub.w	d4,d1	| pre-add height to d1
	subq.w	#1,d1	| minus one
1:	
	tst	d1
    beq.b   2f    | optim
    lea		mulNB_BYTES_PER_ROW_table(pc),a4
	.ifdef	NO68020
	add.w	d1,d1
    move.w  (a4,d1.w),d1	| y times 40
	.else
    move.w  (a4,d1.w*2),d1	| y times 40
	.endif
2:
	lea		cookie_cut_blit_table(pc),a4
	
    move.w  d0,d6
    lsr.w   #3,d0			| X displacement in bytes
	*bclr	#0,d0			| no need, it will be even
    and.w   #0xF,d6
	.ifdef	NO68020
	add.w	d6,d6
	add.w	d6,d6
	move.l	(a4,d6.w),d7
	lea		cookie_cut_mask_blit_table(pc),a4
	move.l	(a4,d6.w),d6
	.else
	move.l	(a4,d6.w*4),d7
	lea		cookie_cut_mask_blit_table(pc),a4
	move.l	(a4,d6.w*4),d6
	.endif
3:   
    add.w   d0,d1
4:
    * make offset even. Blitter will ignore odd address
    * but a 68000 CPU doesn't and since we RETURN A1...
    *bclr    #0,d1
    add.w   d1,a1       | plane position (D1 < 0x7FFF, 288*40=0x2D00)
	move.w	#NB_BYTES_PER_ROW,d0
	tst.w	d4
	bpl.b	5f
	neg.w	d0
	neg.w	d4    | make d4 positive again
5:
    sub.w   d2,d0       | blit width
    lsl.w   #6,d4
    lsr.w   #1,d2
    add.w   d2,d4       | blit height
    * always the same settings (ATM)
	moveq	#0,d2
	add.w	d5,a3			| apply y offset to mask too (y offset looks wrong)
	
    * now just wait for blitter ready to write all registers
	WAIT_BLIT
    
    * blitter registers set

	move.w d2,bltamod(a5)		|A modulo=bytes to skip between lines
	move.w d2,bltbmod(a5)		|B modulo=bytes to skip between lines
	
    move.w  d0,bltcmod(a5)	|C modulo
    move.w  d0,bltdmod(a5)	|D modulo
					
	subq	#1,d3
	beq.b	7f
	subq	#1,d3
6:
	jbsr	process_1_plane
	
	lea		(SCREEN_PLANE_SIZE,a1),a1
	dbf		d3,6b
7:
	jbsr	process_1_plane
    
    movem.l (a7)+,d0-d7/a4
    rts
	
process_1_plane:
	move.l (a0)+,d0
	beq.b	60f
	move.l	d0,a4
	add.w	d5,a4		| add Y offset
	bra.b	61f
60:
	.ifeq	OPT_IGNORE_ZERO_BITPLANE
	WAIT_BLIT
	* source is 0: just apply mask (less bandwidth lost) and change bltcon
	move.l	d6,bltcon0(a5)	| sets con0 and con1: C-A->D cookie cut, B fixed
 	move.w	d2,bltbdat(a5)	|B word is zero
	move.l	a1,bltcpt(a5)	|pristine background top (bottom) left corner
	move.l	a1,bltdpt(a5)	|destination top (bottom) left corner
	move.w  d4,bltsize(a5)	|rectangle size, starts blit
	.endif
	rts
61:
	WAIT_BLIT
	* non-zero: set data source & bltcon
	move.l  a3,bltapt(a5)	|source graphic top left corner (mask)
	move.l	d7,bltcon0(a5)	| sets con0 and con1: C-A+B->D cookie cut full
	move.l	a4,bltbpt(a5)	|source graphic top left corner
	move.l	a1,bltcpt(a5)	|pristine background top (bottom) left corner
	move.l	a1,bltdpt(a5)	|destination top (bottom) left corner
	move.w  d4,bltsize(a5)	|rectangle size, starts blit
	rts

	.macro	GEN_MINTERM_TABLE	name,value
\name\()_table:
	.set	shift,0
	.rept	16
	.long	(shift<<28)+(shift<<12)+(\value<<16)
	.set	shift,shift+1
	.endr
	.endm
	
	.macro	GEN_MINTERM_TABLE_2	name,value
\name\()_table:
	.set	shift,0
	.rept	16
	.long	(shift<<28)+(\value<<16)
	.set	shift,shift+1
	.endr
	.endm
	
	GEN_MINTERM_TABLE	cookie_cut_blit,0x0FCA
	GEN_MINTERM_TABLE_2	cookie_cut_mask_blit,0x0BCA


* < D0,D1: x,y
* < A1: foreground plane pointer (often first plane!)
* < D2: width in bytes (inc. 2 extra for shifting)
* < D3: nb planes to clear
* < D4: blit height
* < D5: plane mask

* trashes D0-D6/A2
clear_planes_any_blitter_internal:
    * pre-compute the maximum of shit here
	move.w	d1,d6
    beq.b   1f    | optim
	* check if y start + height > Y_MAX
	* (ignores case where y = 0, no risk)
	add.w	d4,d6
	sub.w	#Y_MAX,d6
	bmi.b	0f
	* y start + height > Y_MAX: clip height
	sub.w	d6,d4
	beq.b	10f
	bpl.b	0f
10:
	* null or negative height after clipping: out
	rts
0:
    lea mulNB_BYTES_PER_ROW_table(pc),a2
    add.w   d1,d1
    move.w  (a2,d1.w),d1
1:
    and.w   #0x1F0,d0
    beq.b   2f				| zero X optimization
    lsr.w   #3,d0
    add.w   d0,d1
2:   
    add.w   d1,a1       | plane position (always even)

	* reuse d1 for bltcon0
	moveq	#1,d1
	ror.l	#8,d1     | AKA move.l  #0x01000000,d5   | minterm useD & rect clear (0xA) 

	move.w #NB_BYTES_PER_ROW,d0
	bclr	#0,d2		| make sure D2 is even (else strange things occur!)
    sub.w   d2,d0       | blit width

	move.w	d4,d6		| save height
    lsl.w   #6,d4
    lsr.w   #1,d2
    add.w   d2,d4       | blit height+width

    * now just wait for blitter ready to write all registers
	WAIT_BLIT
    
	
	move.l d1,bltcon0(a5)	
    move.w  d0,bltdmod(a5)	|D modulo
	
	subq	#1,d3
	jeq		4f
	subq	#1,d3
	jra		31f
3:
*	lsr.b	#1,d5
*	jcc		32f
	BTST	#6,(dmaconr,a5)
	jeq		31f
	* blitter is busy: do it with cpu for that plane
	move.w	d6,d2
	subq	#1,d2
	move.l	a1,a2
	* no need to know which width in the case of gyruss there are
	* only 32 bit erase / blits
55:
	clr.l	(a2)
	lea		(NB_BYTES_PER_ROW,a2),a2
	dbf		d2,55b
	jra 	32f			| should send to blitter operation next time
	
31:
	move.l a1,bltdpt(a5)	|destination top left corner
	move.w  d4,bltsize(a5)	|rectangle size, starts blit
32:
	lea		(SCREEN_PLANE_SIZE,a1),a1
	dbf		d3,3b
4:
*	lsr.b	#1,d5
*	jcs		5f
*	rts
*5:
	* last plane: if blitter busy, do with CPU
	BTST	#6,(dmaconr,a5)
	jeq		31f
	* blitter is busy: do it with cpu for that plane
	move.w	d6,d2
	subq	#1,d2
	move.l	a1,a2
	* no need to know which width in the case of gyruss there are
	* only 32 bit erase / blits
55:
	clr.l	(a2)
	lea		(NB_BYTES_PER_ROW,a2),a2
	dbf		d2,55b
	rts
31:
	move.l a1,bltdpt(a5)	|destination top left corner
	move.w  d4,bltsize(a5)	|rectangle size, starts blit
    rts


nb_stars:
	.word	0

	
sprite_buffer:
	.skip	NB_TARGET_SPRITES*SPRITE_SIZEOF
	
bob_previous_positions_1:
	ds.b	NB_TARGET_SPRITES*PREVIOUS_SPRITE_SIZEOF
bob_previous_drawed_sprites:
	.long	bob_previous_positions_1
bob_previous_displayed_sprites:
	.ifeq	OPT_ENABLE_DOUBLE_BUFFERING
	.long	bob_previous_positions_1
	.else
	.long	bob_previous_positions_2	
bob_previous_positions_2:
	ds.b	NB_TARGET_SPRITES*PREVIOUS_SPRITE_SIZEOF	
	.endif

		
	.ifne	OPT_ENABLE_DOUBLE_BUFFERING
switch_screen_buffers:

	* let's switch screen buffers, background (chars)
	* toggle fg planes (double buffering)
	* also toggle previous sprite positions
	lea		playfield_drawed_screen_ptr(pc),a0
	lea		bob_previous_positions_1(pc),a2
	lea		bob_previous_positions_2(pc),a3
	move.l	bg_screen_data_1,a1
	move.l	bg_screen_data_2,a5
	* also switch previous state memory
	cmp.l	(a0),a1
	bne.b	2f
	* screen data 1 is displayed screen
	* set it to drawn screen
	exg		a1,a5
	exg		a2,a3
2:
	move.l	a1,(a0)
	move.l	a5,playfield_displayed_screen_ptr
	move.l	a3,bob_previous_displayed_sprites
	move.l	a2,bob_previous_drawed_sprites

	jbra		set_static_bitplanes
	.endif
	
	
store_system:
	tst.l	_resload
	beq.b	0f
	rts
0:
	* system copper
	
	move.l	4.W,a6
	move.w	AttnFlags(a6),d0
	btst	#AFB_68010,d0
	beq.b	1f
	lea	get_vbr,a5

	jsr		_LVOSupervisor(a6)
	move.l	d0,system_vbr
1:
	move.w	AttnFlags(a6),d0
	btst	#AFB_68020,d0
	beq.b	0f				| no 68020: disable stars & switch to 25FPS
	
	* check fastmem
	pea		2f(pc)	
2:
	move.l	(a7)+,d0
	cmp.l	#0x200000,d0
	bcc.b	1f				
0:
	* no whdload, no fastmem/68020: assume slow switch to 25 FPS
	* if running on PiStorm, well, too bad use whdload
	*
	* note: game crawls on vanilla A1200 too if stars are on
	*
	st.b	frameskip
	* also turn off stars
	st.b	stars_off_flag
1:

	lea	_custom,a6
	move.w	intenar(a6),old_intena
	move.w	dmaconr(a6),old_dmacon
	lea		old_sysvectors,a0
	move.l	system_vbr(pc),a1
	move.l	(0x68,a1),(a0)+
	move.l	(0x6c,a1),(a0)+
	move.l	(0x70,a1),(a0)+
	move.l	(0x74,a1),(a0)+
	move.l	(0x78,a1),(a0)+
	move.l	(0x7C,a1),(a0)+
	rts

restore_system:
	jbsr	MixerStop
	jbsr	MixerRemoveHandler
	
	move.l	system_vbr(pc),a0
	lea	_custom,a6
	jsr		_mt_remove_cia

	move.w	#0x4000,intena(a6)	| ints off
	lea		old_sysvectors,a0
	move.l	system_vbr(pc),a1
	move.l	(a0)+,(0x68,a1)
	move.l	(a0)+,(0x6c,a1)
	move.l	(a0)+,(0x70,a1)
	move.l	(a0)+,(0x74,a1)
	move.l	(a0)+,(0x78,a1)
	move.l	(a0)+,(0x7C,a1)


	bclr	#6,0xbfee01

	lea	_custom,a6
	move.w	old_intena,d0			| get old val
	and.w	#0x3fff,d0
	eor.w	#0x3fff,d0
	move.w	d0,intena(a6)		| clr bits
	eor.w	#0x3fff,d0
	or.w	#0xc000,d0		
	move.w	d0,intena(a6)		| set bits and main

	move.w	old_dmacon,d0			| get old val
	and.w	#0x3fff,d0
	eor.w	#0x3fff,d0

***		and.w	#0xfff0,d0		| preserve sound dma for now

	move.w	d0,dmacon(a6)		| clr bits



	eor.w	#0x3fff,d0
	or.w	#0xc000,d0		
	move.w	d0,dmacon(a6)		| set bits and main

	move.l	old_syscoplist,cop1lc(a6)
	move.l	_gfxbase,a6
	move.b	gb_ChipRevBits0(a6),chiprevbits+3
	move.l	old_actiview,a1
	jsr	_LVOLoadView(a6)


	moveq	#0,d0
	rts

quit_to_os:
	tst.l	_resload
	bne.b	0f
	bsr		restore_system
	* set SR to 0 from here, so we return to OS with SR=0
	* (else it kills the system pretty quickly)
	* it works as quit_to_os is called from an interrupt
	move.w	#0,SR
	move.l	old_stack(pc),a7
	jbsr		save_highscores
	moveq	#0,d0
	rts
0:
	pea	  TDREASON_OK
	move.l	_resload(pc),-(a7)
	addq.l	#resload_Abort,(a7)
	rts



	
platform_init:
	bsr		store_system
	

	lea		_custom,a5
	move.w	#0x7FFF,dmacon(a5)
	move.w	#0x7FFF,intena(a5)
	* set blitter parameters once and for all
    move.l  #-1,bltafwm(a5)
	
	move.l	monitor,d0
	cmp.l	#NTSC_MONITOR_ID,D0
	bne		0f
	* disable extra mid-screen interrupt, as it would be too fast (NTSC)
	move.w	#0x1FE,intreq_block_game
0:
    move.w #0x200+(NB_PLAYFIELD_PLANES<<12),bplcon0(a5)

 	*         VVHH
    move.w #0x2499,diwstrt(a5)
    move.w #0x2479,diwstop(a5)
*	cmp.l	#NTSC_MONITOR_ID,monitor
*	jne		0f
*    move.w #0xFC81,diwstop(a5)
*0:	
    move.w #0x0038,ddfstrt(a5)
    move.w #0x00D0,ddfstop(a5)

	* restrict borders: we don't see bobs on the borders!
	* try to hide color 0 on the border (except on OCS)

    move.w #0,bplcon1(a5)
	moveq	#0,d0
    move.w d0,bpl1mod(a5)
    move.w d0,bpl2mod(a5)

	
	lea		_custom,a5
	lea		game_copperlist,a0
	move.l	a0,cop1lc(a5)

	
	* set sprite registers to blank sprite in copperlist
	* we don't use sprites at all in that game
	* it's too complicated to use them because most
	* sprites have more than 3 colors. Blitter manages
	* very well.
	* we could have used attached sprites for main character
	
	lea	game_sprites+2,a2			| copperlist

	* pad last sprite entries with blank
	move.l	#blank_sprite,d0
1:
	move.w	d0,(4,a2)
	swap	d0
	move.w	d0,(a2)
	swap	d0
	addq	#8,a2
	cmp.l	#game_sprites_end,a2
	bcs		1b
2:	



	* wait 300 lines
	move.w	#100,d0
	bsr	beamdelay
	move.w	#100,d0
	bsr	beamdelay
	move.w	#100,d0
	bsr	beamdelay
	
    * init sprite, bitplane, whatever dma

    move.w #0x83E0,dmacon(a5)
 

	* install keyboard and vblank/copper handler
	move.l	system_vbr(pc),a1
	pea		level2_interrupt(pc)
	move.l	(a7)+,(0x68,a1)
	pea		_vblank(pc)
	move.l	(a7)+,(0x6C,a1)
	pea		_nmi(pc)
	move.l	(a7)+,(0x7C,a1)

	* soundfx lib
	lea		_custom,a6
	lea		blank_sound,a0
	move.l	a0,(0xA0,a6)
	move.l	a0,(0xB0,a6)
	move.l	a0,(0xC0,a6)
	move.l	a0,(0xD0,a6)
	move.l	system_vbr(pc),a0
	moveq	#1,d0	| PAL
	jsr		_mt_install_cia


*	moveq	#4,d1
*	* Sample 1
*	lea.l	credit_sound,a2	
*	move.l	(mfx_length,a2),a0 | Sample source
*	move.l	a0,a1					| Pre-processed destination
*	move.l	(mfx_length,a2),d0			| Sample size
*	jbsr		ConvertSampleDivide



	* Setup the mixer
	move.w	#MIX_PAL,d0					| Set video system to PAL
	lea.l	mixer_buffer,a0				| Fetch Chip RAM buffer
	jbsr	MixerSetup					| Set up the mixer

	* Start the mixer interrupt handler
	move.l	system_vbr(pc),a0			| Set VBR
	jbsr	MixerInstallHandler			| Install the mixer IRQ handler

	* Start the mixer
	jbsr		MixerStart

	.ifne	OPT_ENABLE_PROFILING
	* init profiling if enabled
	lea		0x180000,a0
	move.l	#0x20000,d0
	lea		0x100,a1
	bsr		init_fixed_address
	bsr		install_profiler_vbl_hook
	.endif

	
    rts
	
* < A0: palette
* < A1: copperlist
* < D0: number of colors to load	
load_palette:
	subq.w	#1,d0
	beq.b	2f
1:
	addq.w	#2,a1
	move.w	(a0)+,(a1)+
	dbf		d0,1b
2:
	rts
	


	
	

* 60Hz VBLANK interrupt (also keyboard)
* only called at bootup, not from interrupts (at least!!)
osd_interrupt_control:
	tst.b	d0
	jeq		0f
	move.w	#0xC038+0x780,_custom+intena
	rts
0:
	move.w	#0x0030,_custom+intena
	rts

* a0=screen virtual address (preserved), d0=tile (preserved)
* a6=ram base
* should preserve all registers

osd_w_videoram:
	movem.l	d0/d1/d7/a0/a1,-(a7)
	move.l	a0,d7		| save address
	.ifndef	RELEASE
	cmp.l	#0x8800,d7
	jcs		0f
	BREAKPOINT	"write videoram out of range (D7 >= $8800)"
	jra		osd_w_videoram_out
0:
	.endif

	* remove 0x8000 offset (ram starts at 0x8000)
	sub.w	#0x8000,d7
	.ifndef	RELEASE
	jpl	1f

	* should not happen
	add.w	#0x8000,d7
	BREAKPOINT	"write videoram out of range (D7 < $8000)"
	jra		osd_w_videoram_out
	.endif
1:	
	move.l	d7,a0
	add.l	a6,a0				| add RAM base
	move.b	(a0),d1
	cmp.b	d1,d0
	beq.b	osd_w_videoram_out		| already the proper value
3:
	move.b	d0,(a0)	| update logical grid
	
	* now set the flag for tile refresh in copper interrupt (so it doesn't
	* interfere with blits. This game uses a lot of tile animation in the background
	* as opposed to most games, so updating in real tile isn't an option and kills
	* the smoothness even in double buffering mode)
	and.w	#0x3FF,d7

	lea		tiles_refresh_table(pc),a0
	add.w	d7,a0
	lsr.w	#5,d7	| divide by 32
	lea		tiles_row_flags(pc),a1
	add.w	d7,a1
	* now a0 points on the tiles refresh table, mark it dirty
	.ifne	OPT_ENABLE_DOUBLE_BUFFERING
	moveq	#2,d7
	.else
	moveq	#1,d7
	.endif
	move.b	d7,(a0)
	move.b	d7,(a1)

osd_w_videoram_out:
	movem.l	(a7)+,d0/d1/d7/a0/a1
	rts	


* < D7: 0-0x400
* > D0: 
compute_tile_xy_landscape:
	moveq	#0,d0
	move.b	d7,d0
	
	and.b	#0x1F,d0	| x
	move.w	d7,d1
	and.w	#0x3FF,d1
	lsr.w	#5,d1		| Y
	lsl.w	#3,d1		| times 8
	rts

compute_tile_xy:
	moveq	#0,d0
	moveq	#0,d1
	move.w	d7,d1
	lsr.w	#5,d7		| this is X
	neg.w	d7
	add.w	#32,d7
	and.w	#0x1F,d1	| this is Y
	*addq.w	#2,d1

	move.b	d7,d0		| this is X
	lsl.b	#3,d1		| Y times 8
	rts



* addresses 0x8400->0x843F don't map to visible tiles
* addresses 0x87C0->0x87FF don't map to visible tiles
* each column has 0x20*8 = 256 pixels
* each visible row has 28*8 = 244 pixels

* < D7: 0-0x400 address
* < D0: tile code
* < D1: color code

update_bg_tile:
	moveq	#0,d2	| mask up to 255
	move.b	d0,d2			| tile code
	moveq	#0,d3
	move.b	d1,d3			| CLUT & tile code & flags
	
	btst	#5,d3
	jeq		13f
	* add 0x100 to code
	bset	#8,d2
13:
	
	.ifne	OPT_ENABLE_LOGGING
	lea		character_log_table,a0
	movem.w	d2-d3,-(a7)
	and.w	#0xF,d3
	lsl.w	#4,d2		| 16 cluts
	add.w	d2,d3		| clut offset

	st.b	(a0,d3.l)
	movem.w	(a7)+,d2-d3
	.endif

	bsr		compute_tile_xy
	
	lea		character_table,a0

	
	* updating bitplanes
	move.l	#SCREEN_PLANE_SIZE,d5
	move.w	#NB_BOB_PLANES-1,d4
	move.l	playfield_drawed_screen_ptr,a1
	lea		background_restore_buffer,a3


	lea		mulNB_BYTES_PER_ROW_table,A2
	add.w	d1,d1
	move.w	(a2,d1.w),d1
	add.w	d1,d0
	add.w	d0,a1
	add.w	d0,a3


	
	add.w	d2,d2
	add.w	d2,d2	
	move.l	(a0,d2.w),d0		| tile pointer
	.ifdef	RELEASE
	jeq		11f					| clear tile, no error (release)
	.else
	jne		12f
	add.w	#0x8400,d7
	lsr.w	#2,d2
	move.w	#0xF0F,_custom+color
	jra		11f
12:
	.endif
	move.l	d0,a2
	move.b	d3,d1
	and.w	#0xF,d3
	add.w	d3,d3
	add.w	d3,d3
	move.l	(a2,d3.w),d0			| pointer on tile bitplanes for that CLUT
	.ifdef	RELEASE
	jeq		11f						| not necessarily a problem
	.else
	jne		12f
	add.w	#0x8400,d7
	lsr.w	#2,d2
	lsr.w	#2,d3
	move.w	#0xF0F,_custom+color
	jra		11f
	.endif
12:
	move.l	d0,a2					| pointer on bitplanes
	ror.b	#6,d1			        | modifiers on first bits
	and.w	#3,d1
	jeq		standard_tile
	

	add.w	d1,d1
	add.w	d1,d1
	lea		mirror_jump_table(pc),a4
	move.l	(a4,d1.w),a4
0:
	BTST	#6,(_custom+dmaconr)
	BNE.S	0b
	jmp		(a4)
	
	.macro	COPY_TILE_PLANE_ROW
	move.b	d0,(offset,a1)  | buffer
	.endm
	
	.macro	CLEAR_TILE_PLANE
	.set	offset,0
	.rept	8
	clr.b	(offset,a1)
	.set	offset,offset+NB_BYTES_PER_ROW
	.endr	
	.endm
	
	.macro	COPY_TILE_PLANE_ROW_BACKBUFFER
	COPY_TILE_PLANE_ROW
	move.b	d0,(offset,a3)  | backbuffer
	.endm
	
	.macro	CLEAR_TILE_PLANE_BACKBUFFER
	.set	offset,0
	.rept	8
	clr.b	(offset,a1)
	clr.b	(offset,a3)
	.set	offset,offset+NB_BYTES_PER_ROW
	.endr	
	.endm
	
	.macro	TILE_NEXT_PLANE
	add.w	d5,a1
	add.w	d5,a3
	.endm
	
* made more complex by 4 tile orientations + the fact that the last
* tile bitplane should not be copied into the backbuffer as sprites
* only use 4 planes
standard_tile:

	* just copy

4:
	move.l	(a2)+,d0		| next plane
	jeq		5f
	move.l	d0,a0
	.set	offset,0
	.rept	8
	move.b	(a0)+,d0
	COPY_TILE_PLANE_ROW_BACKBUFFER
	.set	offset,offset+NB_BYTES_PER_ROW
	.endr
41:
	TILE_NEXT_PLANE
	dbf		d4,4b
	
	* last plane: no backbuffer
	move.l	(a2)+,d0		| next plane
	jeq		1f				| end
	move.l	d0,a0
	.set	offset,0
	.rept	8
	move.b	(a0)+,d0
	COPY_TILE_PLANE_ROW
	.set	offset,offset+NB_BYTES_PER_ROW
	.endr
	
0:
	rts
1:
	CLEAR_TILE_PLANE
	rts
5:
	CLEAR_TILE_PLANE_BACKBUFFER
	jra		41b

flipped_tile:	
4:
	move.l	(a2)+,d0		| next plane
	jeq		5f
	move.l	d0,a0
	.set	offset,8*NB_BYTES_PER_ROW
	.rept	8
	.set	offset,offset-NB_BYTES_PER_ROW
	move.b	(a0)+,d0
	COPY_TILE_PLANE_ROW_BACKBUFFER
	.endr
41:
	TILE_NEXT_PLANE
	dbf		d4,4b
	
	* last plane
	move.l	(a2)+,d0		| next plane
	jeq		1f
	move.l	d0,a0
	.set	offset,8*NB_BYTES_PER_ROW
	.rept	8
	.set	offset,offset-NB_BYTES_PER_ROW
	move.b	(a0)+,d0
	COPY_TILE_PLANE_ROW
	.endr
	
0:
	rts
1:
	CLEAR_TILE_PLANE
	rts
5:
	CLEAR_TILE_PLANE_BACKBUFFER
	jra		41b
	
mirror_tile:
	lea		mirror_bits(pc),a4
	moveq	#0,d1
4:
	move.l	(a2)+,d0		| next plane
	jeq		5f
	move.l	d0,a0
	.set	offset,0
	.rept	8
	move.b	(a0)+,d1
	move.b	(a4,d1.w),d0	| mirror
	COPY_TILE_PLANE_ROW_BACKBUFFER

	.set	offset,offset+NB_BYTES_PER_ROW
	.endr
41:
	TILE_NEXT_PLANE
	dbf		d4,4b
	
	move.l	(a2)+,d0		| next plane
	jeq		1f
	move.l	d0,a0
	.set	offset,0
	.rept	8
	move.b	(a0)+,d1
	move.b	(a4,d1.w),d0	| mirror
	COPY_TILE_PLANE_ROW

	.set	offset,offset+NB_BYTES_PER_ROW
	.endr
	
0:
	rts
1:
	CLEAR_TILE_PLANE
	rts
5:
	CLEAR_TILE_PLANE_BACKBUFFER
	jra		41b
	
mirror_flipped_tile:
	lea		mirror_bits(pc),a4
	moveq	#0,d1
4:
	move.l	(a2)+,d0		| next plane
	jeq		5f
	move.l	d0,a0
	.set	offset,8*NB_BYTES_PER_ROW
	.rept	8
	.set	offset,offset-NB_BYTES_PER_ROW
	move.b	(a0)+,d1
	move.b	(a4,d1.w),d0	| mirror
	COPY_TILE_PLANE_ROW_BACKBUFFER

	.endr
41:
	TILE_NEXT_PLANE
	dbf		d4,4b

	move.l	(a2)+,d0		| next plane
	jeq		1f
	move.l	d0,a0
	.set	offset,8*NB_BYTES_PER_ROW
	.rept	8
	.set	offset,offset-NB_BYTES_PER_ROW
	move.b	(a0)+,d1
	move.b	(a4,d1.w),d0	| mirror
	COPY_TILE_PLANE_ROW

	.endr

0:
	rts
1:
	CLEAR_TILE_PLANE
	rts
5:
	CLEAR_TILE_PLANE_BACKBUFFER
	jra		41b

	* clear tile

11:
111:
	CLEAR_TILE_PLANE
	TILE_NEXT_PLANE
	dbf		d4,111b
2:
	rts
	

mirror_jump_table:
	.long	standard_tile

	.long	flipped_tile
	.long	mirror_tile
	.long	mirror_flipped_tile
	
mirror_bits:
	.include "mirror_bits.68k"
	
* < d0.w: x
* < d1.w: y
* < a1: sprite pos table (depending on height)
* > d0.L: control word
store_sprite_pos:
	tst.w	d1
	jmi	0f
	cmp.w	#Y_MAX,d1
	jcc	0f

    move.l  a0,-(a7)

    lea	HW_SpriteXTable(pc),a0
	
	.ifdef	NO68020
    add.w	d0,d0
    add.w	d0,d0
    move.l	(a0,d0.w),d0
	move.w	d1,-(a7)
    add.w	d1,d1
    add.w	d1,d1
    or.l	(a1,d1.w),d0
	move.w	(a7)+,d1
	.else
    move.l	(a0,d0.w*4),d0
    or.l	(a1,d1.w*4),d0	
	.endc
    move.l  (a7)+,a0
    rts
0:
	moveq	#0,d0
	rts
	



HW_SpriteXTable:
	.set	reptn,0
	.rept 320
	.set	x,reptn+0x80
	.set reptn,reptn+1
    .byte  0,x>>1,0,x&1
  .endr

	.macro DEF_HW_SPRITE_Y_TABLE	height
HW_SpriteYTable_\height:
  .set	reptn,0
  .rept 288
	.set	ys,reptn+0x1c
	.set	ye,ys+\height
    .byte  ys&255, 0, ye&255, ((ys>>6)&4)+((ye>>7)&2)
	.set	reptn,reptn+1
  .endr
	.endm

	DEF_HW_SPRITE_Y_TABLE	16

set_static_bitplanes:
	movem.l	a0-a1/d0-d2,-(a7)
 	lea	bitplanes+2,a0
    moveq #NB_PLAYFIELD_PLANES-1,d1
 	move.l	#SCREEN_PLANE_SIZE,d2
    move.l	playfield_displayed_screen_ptr(pc),d0
1:
    swap d0
    move.w d0,(a0)
    swap d0
    move.w d0,(4,a0)    
    addq.w  #8,a0
    add.l d2,d0
    dbf d1,1b
	movem.l	(a7)+,a0-a1/d0-d2
    rts

	
osd_debug_hook:
	rts
	
osd_get_random:
* > D0: pseudo random value (8 bits, all other bits zeroed)
	movem.l	a0,-(a7)
	lea	seed(pc),a0
	move.l	(a0),d0
	* thanks meynaf
	mulu #0xa57b,d0
	addi.l #0xbb40e62d,d0
	rol.l #6,d0	
	move.l	d0,(a0) 
	movem.l	(a7)+,a0
	add.b	_custom+vposr,d0
	and.l	#0xFF,d0
	rts
seed:
	dc.l	0x12345678

	
	
	
osd_read_inputs_2:
	moveq	#-1,d0

	rts
	
osd_read_system_inputs:
	moveq	#-1,d0
	movem.l	a0/d1,-(a7)
	lea		keyboard_table(pc),a0
	* misc control keys
	tst.b	(6,a0)		| "6" key like in MAME
	beq.b	0f
	bclr	#BIT_INSERT_COIN_2,d0
0:
	tst.b	(5,a0)		| "5" key like in MAME
	beq.b	0f
	bclr	#BIT_INSERT_COIN_1,d0
0:
	tst.b	(1,a0)		| "1" key like in MAME
	beq.b	0f
	bclr	#BIT_START_1P,d0
0:
	tst.b	(2,a0)		| "2" key like in MAME
	beq.b	0f
	bclr	#BIT_START_2P,d0
0:	

	bsr		is_game_playing
	jne	1f
	move.l	_joypad_state,d1
	* game not playing, we can insert coin/start with joypad
	btst	#JPB_BTN_RED,d1		| "5" key like in MAME
	beq.b	0f
	bclr	#BIT_INSERT_COIN_1,d0
0:
	btst	#JPB_BTN_UP,d1		| "1" key like in MAME
	beq.b	0f
	bclr	#BIT_START_1P,d0
0:
	btst	#JPB_BTN_DOWN,d1		| "2" key like in MAME
	beq.b	0f
	bclr	#BIT_START_2P,d0
0:	
1:	
	movem.l	(a7)+,a0/d1
	rts
	
osd_read_inputs_1:
	moveq	#-1,d0
	movem.l	a0/d1,-(a7)


	lea		keyboard_table(pc),a0

	tst.b	(0x4C,a0)		| "up" key
	beq.b	0f
	bclr	#BIT_UP,d0
0:
	tst.b	(0x4D,a0)		| "down" key
	beq.b	0f
	bclr	#BIT_DOWN,d0
0:
	tst.b	(0x4F,a0)		| "<-" key
	beq.b	0f
	bclr	#BIT_LEFT,d0
0:
	tst.b	(0x4E,a0)		| "->" key
	beq.b	0f
	bclr	#BIT_RIGHT,d0
0:
	tst.b	(0x63,a0)		| "ctrl" key
	beq.b	0f
	bclr	#BIT_FIRE,d0
0:
*	tst.b	(0x64,a0)		| left "alt" key
*	beq.b	0f
*	bclr	#BIT_JUMP,d0
*0:

	* directions + fire
	move.l	_joypad_state(pc),d1
	btst	#JPB_BTN_UP,d1
	beq.b	0f
	bclr	#BIT_UP,d0		| only if up for jump set!
0:
	btst	#JPB_BTN_DOWN,d1
	beq.b	0f
	bclr	#BIT_DOWN,d0
0:
	btst	#JPB_BTN_LEFT,d1
	beq.b	0f
	bclr	#0,d0
0:
	btst	#JPB_BTN_RIGHT,d1
	beq.b	0f
	bclr	#BIT_RIGHT,d0
0:
	btst	#JPB_BTN_RED,d1
	beq.b	0f
	bclr	#BIT_FIRE,d0
0:
*	btst	#JPB_BTN_BLU,d1
*	beq.b	0f
*	bclr	#BIT_JUMP,d0
*0:

	movem.l	(a7)+,a0/d1
	rts
	


	
osd_read_dsw_1:
	* actual dipswitches
	move.b	dip_switches_1(pc),d0
	not.b	d0		| active low
	rts
osd_read_dsw_2:
	* actual dipswitches
	move.b	dip_switches_2(pc),d0
	not.b	d0		| active low
	rts
osd_read_dsw_3:
	* actual dipswitches
	moveq	#-1,d0
*	move.b	dip_switches_0(pc),d0
*	not.b	d0		| active low
	rts

	


	

level2_interrupt:
	bsr		handle_keyboard
	move.w	#8,_custom+intreq
	rte
	
handle_keyboard:
	movem.l	D0/A0/A5,-(a7)
	LEA	0x00BFD000,A5
	MOVEQ	#0x08,D0
	AND.B	0x1D01(A5),D0
	jeq		1f
	MOVE.B	0x1C01(A5),D0
	NOT.B	D0
	ROR.B	#1,D0		| raw key code here
  
	CLR.B	0x1C01(A5)
  
    lea keyboard_table(pc),a0
    bclr    #7,d0
    seq (a0,d0.w)       | updates keyboard table
    jne   2f     | we don't care about key release
	
    cmp.b   #0x19,d0	| "P" key
    bne.b   0f

    bsr	toggle_pause
0:

	btst	#4,cheat_flags+3
	jeq		10f
	* cheat keys
	cmp.b	#0x50,d0	| F1 key: only 1 wolf
	bne.b	0f
	move.w	#0x0FF,_custom+color
	st.b 	level_skip_flag
	st.b	cheat_used
	
0:
	cmp.b	#0x51,d0	| F2 key
	bne.b	0f
	st.b	cheat_used
	move.w	#0x0FF,_custom+color
	
0:
	cmp.b	#0x52,d0	| F3 key
	bne.b	0f
	st.b	cheat_used
	* add 10000 points
	move.w	#0x0FF,_custom+color
	jbsr	add_10000_points
	
0:
	cmp.b	#0x53,d0	| F4 key
	bne.b	0f
	st.b	cheat_used
	move.w	#0x0FF,_custom+color
	eor.b	#1,invincible_flag
0:
*	cmp.b	#0x54,d0	| F5 key
*	bne.b	0f
*	st.b	cheat_used
*	move.w	#0x0FF,_custom+color
*0:
*
10:
	.ifndef	RELEASE

	.ifne	OPT_ENABLE_LOGGING
	cmp.b	#34,d0
	bne		0f
	* 'D': dump all logging buffers to data dir
	jbsr	osd_dump_logs
0:
	.endif
	cmp.b	#19,d0
	bne		0f
	* 'R': reset start level at 1
	move.w	#0x0FF,_custom+color
	clr.b	start_level
0:
	
*	cmp.b	#0x56,d0	| F7 key: lose all l
*	bne.b	0f
*	move.w	#0x0FF,_custom+color
*0:
	* last life, if killed, direct game over
	cmp.b	#0x57,d0	| F8 key: lose all lives
	bne.b	0f
	move.w	#0x0FF,_custom+color
	* last life, if killed, direct game over
	bsr		lose_all_lives
0:
	cmp.b	#0x58,d0	| F9 key crashes game (restarts whdload)
	bne.b	0f
	illegal
0:

	.endif

	cmp.b	_keyexit,d0	| "ESC" key or whdload quitkey
	bne.b	2f
	BSET	#0x06,0x1E01(A5)
	moveq	#2,d0
	bsr	beamdelay
	BCLR	#0x06,0x1E01(A5)	| acknowledge key
	bra		quit_to_os
2:
	BSET	#0x06,0x1E01(A5)
	moveq	#2,d0
	bsr	beamdelay
	BCLR	#0x06,0x1E01(A5)	| acknowledge key

1:
	movem.l	(a7)+,d0/a0/a5
	rts
	
toggle_pause:
	* pause only within game
	jbsr		is_game_playing
	beq.b	0f
	eor.b   #1,pause_flag
0:
	rts

	

    
* < D0: numbers of vertical positions to wait
beamdelay:
0:
	move.w  d0,-(a7)
    move.b	0xdff006,d0	| VPOS
1:
	cmp.b	0xdff006,d0
	beq.s	1b
	move.w	(a7)+,d0
	dbf	d0,0b
	rts

	
	MUL_TABLE	NB_BYTES_PER_ROW,NB_LINES*2

* direct page pointer is set by gyruss reset to 0x42
* then it's never changed
m6809_direct_page_pointer:
	.long	0xA4A4A4A4




	.align	4		| leave this long word alignment
program_args:	ds.l	64,0
program_args_end:
	.long	0		| add a longword else it overwrites the read args string in kick 1.3!
	.align	4
read_args_string:
	.byte	read_args_string_end-read_args_string
	.ascii	"INVINCIBLE/S,INFLIVES/S,"
	.ascii	"CHEATKEYS/S,STARTLIVES/K/N,SKILL/K/N,STARTLEVEL/K/N"
read_args_string_end:
	.align	2


	.ifndef	RELEASE
sprite_allocation_error:
	.asciz	"sprite allocation error"
write_videoram_out_of_range_error:
	.asciz	"write videoram out of range (D7)"
write_colorram_out_of_range_error:
	.asciz	"write colorram out of range (D7)"

	.align	2
	.endif

old_intena:
	.long	0
old_dmacon:
	.long	0
old_sysvectors:
	ds.l	10
old_vbr:
	.long	0
old_syscoplist:
	.long	0
old_actiview:
	.long	0
old_stack:
	.long	0

	
_tag:
		.long	WHDLTAG_MONITOR_GET
monitor:
		.long	0
		.long	WHDLTAG_CHIPREVBITS_GET
chiprevbits:
		.long	0
		.long	WHDLTAG_CUSTOM1_GET
cheat_flags:
		.long	0

		.long	WHDLTAG_CUSTOM2_GET
misc_options:
		.long	0
		
		.long	WHDLTAG_CUSTOM3_GET
start_level_option:
		.long	0
		
		.long	WHDLTAG_CUSTOM4_GET
start_lives:
		.long	0

		.long	WHDLTAG_CUSTOM5_GET
difficulty_level:
		.long	0
	.long	0
dip_switches_1:
		.byte	0
dip_switches_2:
		.byte	0

start_level_table:
	.byte	0
	.byte	1
	.byte	3
	.byte	4
	.byte	5
	.byte	7
	.byte	8
	.byte	9
	.byte	11
	.byte	12
	.byte	13
	.byte	15
	.byte	16
	.byte	17
	.byte	19
	.byte	20
	.byte	21
	.byte	23
	.byte	24
	.rept	40
	.byte	0
	.endr
	
mid_screen_interrupt_count:
	.byte	0
	.align	2
system_vbr:
	.long	0


flip_flop:
	.byte	0


	.align	2

host_sprites:
	ds.b	8*SPRITE_SIZEOF
keyboard_table:
	ds.b	0x100

interrupt_mode:
	.word	0
	
pause_flag:
	.word	0
frameskip:
	.word	0

tiles_row_flags:
	.skip	0x20
tiles_refresh_table:
	.skip	0x400
	
vbl_counter:
	.word	0
_resload:
	.long	0
_joypad_state:
	.long	0
_previous_joypad_state:
	.long	0
_gfxbase:
	.long	0
_dosbase:
	.long	0
delay_timer:
	.word	0
one_out_of_five:
	.word	0


bg_screen_data_1:
	.long	0
	.ifne	OPT_ENABLE_DOUBLE_BUFFERING
bg_screen_data_2:
	.long	0
	.endif
	
	

playfield_drawed_screen_ptr:
	.long	0
playfield_displayed_screen_ptr:
	.long	0

queued_sprite_buffer:
	.skip	0x200
	
queued_sprite_pointer:
	.long	0

	

	.include	"sound_entries.68k"

current_column_scroll_values:
	.long	column_scroll_values_1
previous_column_scroll_values:
	.long	column_scroll_values_2
	
* first current scroll value then previous one
column_scroll_values_1:
	.skip	16
column_scroll_values_2:
	.skip	16
	
update_tile_function:
	.long	0
_keyexit:
	.byte	0x45	| ESC by default unless whdload overwrites it

main_scroll_value:
	.byte	0

	
cheat_used:
	.byte	0
dosname:
	.ascii	"dos.library"
	.byte	0
graphicsname:
	.ascii	"graphics.library"
	.byte	0
floppy_file:
	.ascii	"floppy"
	.byte	0
temp_filename_buffer:
	ds.b	255
_end_of_ng_code:
		
  

	.align 2



palette:	
	.include "palette.68k"

	.ifndef	RELEASE
log_ptr:
	.long	log_buffer
sub_log_ptr:
	.long	sub_log_buffer
log_buffer:
	.skip	LOG_BUFFER_SIZE
	.rept	20
	.long	0xCAFECAFE
	.endr
sub_log_buffer:
	.skip	LOG_BUFFER_SIZE
	.rept	20
	.long	0xCAFECAFE
	.endr
cpu_log_enable:
	.word	0
	.endif

	
	* on amiga, this file is included in mpatrol.68k file
	* so the assembler can optimize by using PC-relative addressing
	* whenever possible (and it does)
	.ifndef	RELEASE
	* easier to debug game to have memory aligned on 0x10000

	.ifne	OPT_ENABLE_LOGGING
	.section	.bss

sprite_log_table:
	* 16 cluts, 512 tiles
	* a lot of combinations aren't used (fortunately!)
	ds.b	16*512
character_log_table:
	* 16 cluts, 512 tiles
	* a lot of combinations aren't used (fortunately!)
	ds.b	16*512
	.endif
	.endif



	.section	.datachip

* title
.macro	DECL_BITPLANES	bpoff_start,nb_planes
	.set	bpoff,\bpoff_start
	.rept	\nb_planes
	dc.w  0xe0+bpoff,0
	dc.w  0xe2+bpoff,0
	.set	bpoff,bpoff+4
	.endr
	.endm

	
.macro	DECL_COLORS	offset,nb_colors
	.set	colidx,\offset
	.rept	\nb_colors
	dc.w	color+(colidx*2),0
	.set	colidx,colidx+1
	.endr
	.endm

.macro	DECL_SPRITES
	.set	sprite_start,sprpt+0
	.rept	16
	* 2 slots
	.word	sprite_start,0
	.set	sprite_start,sprite_start+2
    .endr
	.endm

blank_sprite:
	.long	0,0
	
blank_sound:
	ds.l	4
	
debug_copperlist:
	dc.w	bplcon0,0x200
	dc.w	color
debug_color:
	dc.w	0
	dc.l	-2
	


	
	* main copperlist
game_copperlist:
	* all sprites have the same palette no need to specify odd or even
	* on color bank 1
	
	* black color background plane

	* enable sprite colors now

bitplanes:
	DECL_BITPLANES	0,NB_PLAYFIELD_PLANES

*game_sprite_colors:
*	DECL_COLORS	16,16
game_sprites:
	DECL_SPRITES
game_sprites_end:

game_colors:
	DECL_COLORS	0,TOTAL_NB_COLORS


	* priority: BOBs playfield on top
	.word	bplcon2,BASE_BPLCON2_VALUE



	
	* mid-line wait & extra interrupt
	.word	0x8001,0xFFFE
intreq_block_game:
	* now wait some lines until we're sure bitmap only contains
	* non-black: we can set color 0 to light blue now
	.word	 intreq,0x8014  | call copper interrupt in the middle of a frame, also flag so copper interrupt knows that we're going to call copper int

	* purple 


	* pal limit
    .word  0xFFDF,0xFFFE       | PAL wait
	.word	0x1601,0xFFFE

	| no bobs from now on, get a LOT of drawing time
	.word	 intreq,0x8010

	* black color sprites
	*DECL_COLORS	16,16


	
     .long    -2					
	.ascii	"ENDCPLST"

	* aligning on 8 bytes so .align will work
	* in the next datachip sections
	.align	8
	
		.section .bsschip
		

mixer_buffer:
	.skip 	mixer_buffer_size
	
	
background_restore_buffer:
	.skip	RESTORE_SCREEN_SIZE
		
bg_screen_buffer_1:
	.skip	SCREEN_SIZE
	*.skip	NB_BYTES_PER_ROW*12*3
	.ifne	OPT_ENABLE_DOUBLE_BUFFERING
bg_screen_buffer_2:
	.skip	SCREEN_SIZE
	.skip	NB_BYTES_PER_ROW*12*3
	.endif


		